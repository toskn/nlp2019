define Consonant b | c | d | f | g | h | j | k | l |
                 m | n | p | q | r | s | t | v | w | x | z ;
define Vowel a | o | u | e | i | y ;
define Letter Vowel | Consonant;
define POS "+N" | "+V";
define Form "+Smpl" | "+PastPart" | "+Past" | "+PresPart" | "+Sg" |
            "+Pres3Sg" | "+Pl";
define Input Letter+ POS Form;

# Define inflections
define Inflections "+N" "+Sg" -> 0, "+N" "+Pl" -> "^" s , 
                    "+V" "+Smpl" -> 0 , "+V" "+Pres3Sg" -> "^" s,
                    "+V" "+Past" -> "^" e d, "+V" "+PastPart" -> "^" e d,
                    "+V" "+PresPart" -> "^" i n g || _ ;

# Consonant doubling: 1-letter consonant doubled before -ing/-ed (beg/begging)
define ConsonantDoubling g -> g g || _ "^" [i n g | e d ];

# E deletion: silent e dropped before -ing and -ed (make/making)
define EDeletion e -> 0 || _ "^" [ i n g | e d ] ;

# E insertion e added after -s, -z, -x, -ch, -sh before s (watch/watches)
define EInsertion [..] -> e || s | z | x | c h | s h _ "^" s ;

# Y replacement: -y changes to -ie before -s, -i before -ed (try/tries)
define YReplacement y -> i e || Consonant _ "^" s    ,,
                    y -> i   || Consonant _ "^" e d  ;

# K insertion: verbs ending with vowel + -c add -k (panic/panicked)
define KInsertion [..] -> k || Vowel c _ "^" [e d | i n g];

# Exceptions
define Exceptions [[{make} "+V" "+Past" .x. {made}] | 
                   [{make} "+V" "+PastPart" .x. {made}];

# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

define Regular Input .o.
               Inflections .o.
               ConsonantDoubling .o.
               EDeletion         .o.
               EInsertion        .o.
               YReplacement      .o.
               KInsertion        .o.
               Cleanup;
define Grammar Exceptions .P. Regular ;
push Grammar
